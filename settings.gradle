pluginManagement {
	includeBuild 'gradle/plugins/jbake-gradle-plugin'
}

rootProject.name = 'dotgithub'

/**
 * Adds specified component path to this Gradle build.
 *
 * @param path  the component path under {@literal components} folder, must not be null
 */
void component(String path) {
	def tokens = path.split('/')
	def projectPath = tokens.join(':')
	include(projectPath)
	project(":${projectPath}").projectDir = file("components/${path}")
	project(":${projectPath}").buildFileName = "${tokens.join('-')}.gradle"
}

component('favicon')
component('menu')
component('title')
component('meta-page-information')
component('meta-open-graph')
component('meta-twitter-card')
component('meta-encoding')
component('multi-language-sample')

gradle.allprojects {
	group = 'dev.nokee.docs'
	version = '1.0'

	repositories {
		mavenCentral()
	}

	pluginManager.withPlugin('maven-publish') {
		publishing {
			repositories {
				maven { url = "${rootDir}/repo" }
			}
		}
	}
}