pluginManagement {
	includeBuild 'gradle/plugins/jbake-gradle-plugin'
}

rootProject.name = 'dotgithub'

/**
 * Adds specified component path to this Gradle build.
 *
 * @param path  the component path under {@literal components} folder, must not be null
 */
void component(String path) {
	def tokens = path.split('/')
	def projectPath = tokens.join(':')
	include(projectPath)
	project(":${projectPath}").projectDir = file("components/${path}")
	project(":${projectPath}").buildFileName = "${tokens.join('-')}.gradle"
}

component('asciidoc')
component('favicon')
component('menu')
component('meta-page-information')
component('meta-open-graph')
component('meta-twitter-card')
component('meta-encoding')
component('multi-language-sample')
component('layout-main')
component('layout-page')

// Use date/time as the version because commits mess-up the "latest" search
def globalVersion = new java.text.SimpleDateFormat("yyyyMMddHHmm").format(new Date())
gradle.allprojects {
	group = 'dev.nokee.docs'
	version = globalVersion

	repositories {
		mavenCentral()
	}

	pluginManager.withPlugin('maven-publish') {
		publishing {
			repositories {
				maven {
					name = 'docsNokeeDev'
					credentials(AwsCredentials)
					url = 's3://docs.nokee.dev/'
				}
			}
		}
	}
}